plugins {
    id 'maven-publish'
}

// Resolver versi√≥n efectiva: usa -Pversion si viene de JitPack/CLI, si no VERSION_NAME
def propVersion = project.findProperty('version')
def effectiveVersion = (propVersion != null && propVersion.toString() != 'unspecified') 
    ? propVersion.toString() 
    : (findProperty('VERSION_NAME') ?: '5.0.0').toString()

group = 'com.github.bazookon'
version = effectiveVersion

// Tarea para asegurar que el archivo AAR existe
task verifyAarFile {
    doFirst {
    def aarFile = file("aar/nativesdk-${effectiveVersion}.aar")
        if (!aarFile.exists()) {
            throw new RuntimeException("AAR file not found: ${aarFile.absolutePath}")
        }
    }
}

// Tarea assemble requerida por JitPack
task assemble {
    dependsOn verifyAarFile
    group = 'build'
    description = 'Assembles the outputs of this project.'
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'com.github.bazookon'
            artifactId = 'mpago_nativesdk'
            version = effectiveVersion

            artifact("aar/nativesdk-${effectiveVersion}.aar") {
                extension 'aar'
            }

            pom {
                name = 'MercadoPago Native SDK'
                description = 'MercadoPago Native SDK for Android'
                url = 'https://github.com/bazookon/mpago_nativesdk'
                
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                
                developers {
                    developer {
                        id = 'bazookon'
                        name = 'Bazookon'
                    }
                }
                
                scm {
                    connection = 'scm:git:git://github.com/bazookon/mpago_nativesdk.git'
                    developerConnection = 'scm:git:ssh://github.com/bazookon/mpago_nativesdk.git'
                    url = 'https://github.com/bazookon/mpago_nativesdk'
                }
            }
        }
    }
}
